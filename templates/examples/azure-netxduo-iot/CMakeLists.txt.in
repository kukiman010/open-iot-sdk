# Copyright (c) 2022, Arm Limited and Contributors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

cmake_minimum_required(VERSION 3.21.0 FATAL_ERROR)

# Set config variables for the project and dependencies
# Note: NXD_ENABLE_AZURE_IOT and NXD_ENABLE_FILE_SERVERS are set as CACHE
# variables, because netxduo calls cmake_minimum_required() with a very old
# version of CMake that does not allow overriding CACHE configuration variables
# with regular, non-CACHE ones.
set(CMAKE_SYSTEM_PROCESSOR @CMAKE_SYSTEM_PROCESSOR@)
set(IOTSDK_FETCH_LIST
    netxduo
    @RTOS_FETCH@
    @PLATFORM_FETCH@
)
set(NXD_ENABLE_AZURE_IOT ON CACHE BOOL "")
set(NXD_ENABLE_FILE_SERVERS OFF CACHE BOOL "")
set(NX_USER_FILE "${CMAKE_CURRENT_LIST_DIR}/netxduo-config/nx_user.h")
set(VARIANT "FVP")
@RTOS_CONFIG_SET@

include(FetchContent)

# Toolchain files need to exist before first call to project
FetchContent_Declare(iotsdk-toolchains
    GIT_REPOSITORY  @TOOLCHAINS_URL@
    GIT_TAG         @TOOLCHAINS_TAG@
    SOURCE_DIR      ${CMAKE_BINARY_DIR}/toolchains
)
FetchContent_MakeAvailable(iotsdk-toolchains)

# Declare top-level project
project(iotsdk-example-azure-netxduo-iot)

# Set executable suffix to be toolchain-independent for ease of documentation
# and testing. This needs to be done after first call to project().
set(CMAKE_EXECUTABLE_SUFFIX .elf)

# Add Open IoT SDK
FetchContent_Declare(open-iot-sdk
    GIT_REPOSITORY  @IOTSDK_URL@
    GIT_TAG         @IOTSDK_TAG@
)
FetchContent_MakeAvailable(open-iot-sdk)

# Add Open IoT SDK modules to path
list(APPEND CMAKE_MODULE_PATH ${open-iot-sdk_SOURCE_DIR}/cmake)

# Add the project_options tool for code quality checks on this example.
# In a user's own project, the use of project_options is entirely optional.
FetchContent_Declare(project_options
    GIT_REPOSITORY  @PROJECT_OPTIONS_URL@
    GIT_TAG         @PROJECT_OPTIONS_TAG@
)
FetchContent_MakeAvailable(project_options)
include(${project_options_SOURCE_DIR}/Index.cmake)
include(${project_options_SOURCE_DIR}/src/DynamicProjectOptions.cmake)

# Apply Open IoT SDK's default compiler warnings to this example via project_options
include(${open-iot-sdk_SOURCE_DIR}/cmake/AddToolchainWarningFlags.cmake)
set(COMPILER_WARNINGS ${IOTSDK_WARNING_FLAGS} ${IOTSDK_WARNING_AS_ERRORS_FLAGS})
dynamic_project_options(
    CLANG_WARNINGS  ${COMPILER_WARNINGS}
    GCC_WARNINGS    ${COMPILER_WARNINGS}
)

include(ConvertElfToBin)
include(CTest)

# Add example application
add_executable(iotsdk-example-azure-netxduo-iot
    main.c

    sample_azure_iot_embedded_sdk_connect.c
    sample_azure_iot_embedded_sdk.c

    nx_azure_iot_cert.c
    nx_azure_iot_ciphersuites.c
)

target_link_libraries(iotsdk-example-azure-netxduo-iot
    nx-ip-link-mdh

    mcu-driver-hal

    iotsdk-serial-retarget
    iotsdk-emac-factory

    @RTOS_LIBS@
    @PLATFORM_LIBS@
    @PLATFORM_STARTUP@
)

if(CMAKE_C_COMPILER_ID STREQUAL "GNU")
    set(linker_script ${CMAKE_CURRENT_LIST_DIR}/gcc.ld)
    target_link_options(iotsdk-example-azure-netxduo-iot PRIVATE -T ${linker_script})
elseif(CMAKE_C_COMPILER_ID STREQUAL "ARMClang")
    set(linker_script ${CMAKE_CURRENT_LIST_DIR}/armclang.sct)
    target_link_options(iotsdk-example-azure-netxduo-iot PRIVATE --scatter=${linker_script})
endif()

set_target_properties(iotsdk-example-azure-netxduo-iot PROPERTIES LINK_DEPENDS ${linker_script})
target_elf_to_bin(iotsdk-example-azure-netxduo-iot)

add_test(
    NAME    iotsdk-example-azure-netxduo-iot
    COMMAND htrun
        --image-path=iotsdk-example-azure-netxduo-iot@HTRUN_IMG_FORMAT@
        --compare-log=${CMAKE_CURRENT_LIST_DIR}/test.log
        @HTRUN_COMMON_ARGUMENTS@
        @HTRUN_ARGS@
    COMMAND_EXPAND_LISTS
)
