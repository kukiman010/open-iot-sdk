# Copyright (c) 2022-2023, Arm Limited and Contributors. All rights reserved.
# SPDX-License-Identifier: Apache-2.0

cmake_minimum_required(VERSION 3.21.0 FATAL_ERROR)

# Set config variables for the project and dependencies
set(CMAKE_SYSTEM_PROCESSOR @CMAKE_SYSTEM_PROCESSOR@)
set(CMAKE_CXX_STANDARD 17)
set(IOTSDK_FETCH_LIST
    pigweed
    pyedmgr
    nimble
    @RTOS_FETCH@
    @PLATFORM_FETCH@
)
@RTOS_CONFIG_SET@

include(FetchContent)

# Toolchain files need to exist before first call to project
FetchContent_Declare(iotsdk-toolchains
    GIT_REPOSITORY  @TOOLCHAINS_URL@
    GIT_TAG         @TOOLCHAINS_TAG@
    SOURCE_DIR      ${CMAKE_BINARY_DIR}/toolchains
)
FetchContent_MakeAvailable(iotsdk-toolchains)

# Declare top-level project
project(iotsdk-example-pigweed-sys-io)

# Set executable suffix to be toolchain-independent for ease of documentation
# and testing. This needs to be done after first call to project().
set(CMAKE_EXECUTABLE_SUFFIX .elf)

# Set Pigweed backends before SDK fetch.
function(set_pw_backend FACADE BACKEND)
    set(${FACADE}_BACKEND ${BACKEND} CACHE STRING "" FORCE)
endfunction()
set_pw_backend(pw_assert.assert pw_assert_log.assert_backend)
set_pw_backend(pw_assert.check pw_assert_log.check_backend)
set_pw_backend(pw_log pw_log_sys_io_mdh)
set_pw_backend(pw_sys_io pw_sys_io_mcu_driver_hal)

# Add Open IoT SDK
FetchContent_Declare(open-iot-sdk
    GIT_REPOSITORY  @IOTSDK_URL@
    GIT_TAG         @IOTSDK_TAG@
)
FetchContent_MakeAvailable(open-iot-sdk)

# Add Open IoT SDK modules to path
list(APPEND CMAKE_MODULE_PATH ${open-iot-sdk_SOURCE_DIR}/cmake)

# Add the project_options tool for code quality checks on this example.
# In a user's own project, the use of project_options is entirely optional.
FetchContent_Declare(project_options
    GIT_REPOSITORY  @PROJECT_OPTIONS_URL@
    GIT_TAG         @PROJECT_OPTIONS_TAG@
)
FetchContent_MakeAvailable(project_options)
include(${project_options_SOURCE_DIR}/Index.cmake)
include(${project_options_SOURCE_DIR}/src/DynamicProjectOptions.cmake)

# Apply Open IoT SDK's default compiler warnings to this example via project_options
include(${open-iot-sdk_SOURCE_DIR}/cmake/AddToolchainWarningFlags.cmake)
set(COMPILER_WARNINGS ${IOTSDK_WARNING_FLAGS} ${IOTSDK_WARNING_AS_ERRORS_FLAGS})
dynamic_project_options(
    CLANG_WARNINGS  ${COMPILER_WARNINGS}
    GCC_WARNINGS    ${COMPILER_WARNINGS}
)

include(ConvertElfToBin)
include(CTest)

# Configure component properties

# CMSIS 5 require projects to provide configuration macros via RTE_Components.h
# and CMSIS_device_header. The macro CMSIS_device_header is not automatically set
# based on CMAKE_SYSTEM_PROCESSOR in the place where cmsis-core is first defined,
# because a project may want to provide its own device header.
target_compile_definitions(cmsis-core INTERFACE CMSIS_device_header="@CMSIS_device_header@")

target_compile_definitions(pw_iot_sdk_config
    INTERFACE
        PW_IOTSDK_CMSIS_RTX=$<IN_LIST:cmsis-rtx,IOTSDK_FETCH_LIST>
)

# Add RTOS configuration headers
@RTOS_HEADER_INCLUDE@

# Link cmsis-rtos-api against a concrete implementation
target_link_libraries(cmsis-rtos-api
    PUBLIC
        @RTOS_LIBS@
)

function(set_linker_script target)
    if(CMAKE_C_COMPILER_ID STREQUAL "GNU")
        set(linker_script ${CMAKE_CURRENT_SOURCE_DIR}/gcc.ld)
        target_link_options(${target} PRIVATE -T ${linker_script})
    elseif(CMAKE_C_COMPILER_ID STREQUAL "ARMClang")
        set(linker_script ${CMAKE_CURRENT_SOURCE_DIR}/armclang.sct)
        target_link_options(${target} PRIVATE --scatter=${linker_script})
    endif()
    set_target_properties(${target} PROPERTIES LINK_DEPENDS ${linker_script})
endfunction()

target_include_directories(pw_iot_sdk_config INTERFACE pw-config)
target_compile_definitions(pw_iot_sdk_config INTERFACE pw_iot_sdk_config_FILE="pw_iotsdk_config.h")

target_link_libraries(pw_log INTERFACE pw_log_sys_io_mdh)
target_link_libraries(pw_sys_io_mcu_driver_hal PRIVATE pw_sys_io_mdh_cmsis_rtos_lock)

add_executable(iotsdk-example-pigweed-sys-io main.cc uart.cc)
target_link_libraries(iotsdk-example-pigweed-sys-io
    PRIVATE
        pw_assert.assert
        pw_assert.check
        pw_log

        cmsis-rtos-api
        @PLATFORM_LIBS@
        @PLATFORM_STARTUP@

        iotsdk-serial-retarget

        project_options
        project_warnings
)
set_linker_script(iotsdk-example-pigweed-sys-io)
target_elf_to_bin(iotsdk-example-pigweed-sys-io)

add_test(
    NAME    iotsdk-example-pigweed-sys-io
    COMMAND bash
            ${CMAKE_CURRENT_SOURCE_DIR}/example/run-example
            @PLATFORM_FVP@
            $ENV{PYEDMGR_ROOT}
            ${CMAKE_CURRENT_BINARY_DIR}/iotsdk-example-pigweed-sys-io.elf
)
